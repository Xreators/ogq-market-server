buildscript {
    ext {
        set('springBootVersion', '2.4.1')
        set('springCloudVersion', "2020.0.0-M6") // 2.4.x (with springboot 2.4.x)
        set('springCloudBootstrapVersion', "3.0.0-M6") // with spring-cloud 2020.x.x(aka ilford)
        set('springSecurityVersion', '5.4.2')
        set('springDocOpenApiVersion', '1.5.0')
        set('jwtVersion', '3.11.0')
        set('amazonSdkVersion', '2.15.19')
        set('amazonJavaSdkVersion', '1.11.892')
        set('rabbitmqVersion', '5.10.0')
        set('openFeignVersion', '11.0')

        set('gradleDependencyVersion', '1.0.10.RELEASE')
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${gradleDependencyVersion}")
    }
}
allprojects {
    group 'com.ogqcorp.market'
    version '0.1'
}
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        all {
            // exclude group: "org.springframework.boot", module : "spring-boot-starter-logging"
        }
    }

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        implementation platform("org.springframework.security:spring-security-bom:${springSecurityVersion}")

        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-logging'

        // implementation 'org.slf4j:slf4j-api:1.7.30'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        test {
            useJUnitPlatform()
        }
    }
}

project(':common') {
    dependencies {}
}
project(':domain') {
    dependencies {
        implementation project(':common')
    }
}
project(':infrastructure') {
    dependencies {
        implementation project(':common')
    }
}
project(':ontology') {
    dependencies {
        implementation project(':common')
    }
}
project(':repository') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
    }
}
project(':hash-api') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
        implementation project(':repository')
        implementation project(':infrastructure')
    }
}
project(':relay-api') {
    dependencies {
        implementation project(':common')
        implementation project(':domain')
        implementation project(':repository')
        implementation project(':infrastructure')
        implementation project(':ontology')
    }
}